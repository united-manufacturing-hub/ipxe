#!ipxe

:menu License
item --gap -- This software was build using iPXE (https://ipxe.org)
item --gap -- iPXE is free software: you can redistribute it and/or modify
item --gap -- it under the terms of the GNU General Public License as published by
item --gap -- the Free Software Foundation, either version 2 of the License, or
item --gap -- (at your option) any later version.
item --gap -- iPXE is distributed in the hope that it will be useful,
item --gap -- but WITHOUT ANY WARRANTY; without even the implied warranty of
item --gap -- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
item --gap -- GNU General Public License for more details.
item --gap -- You should have received a copy of the GNU General Public License
item --gap -- along with iPXE.  If not, see <http://www.gnu.org/licenses/>.
item --gap --
item --key c continue (C)ontinue [Default]
item --key r reboot (R)eboot
choose --default continue --timeout 30000 target && goto ${target} || reboot

:menu
menu Networking
item --gap -- Serial: ${serial}
item --gap -- MAC: ${mac}
item --gap -- UUID: ${uuid}
item --key d dhcpip (D)HCP IP [Default]
item --key s staticip (S)tatic IP
item --key a advancedip (A)dvanced IP settings
choose --default dhcpip --timeout 30000 target && goto ${target} || reboot

:dhcpip
dhcp && goto validateip || goto dhcperror

:dhcperror
menu DHCP failure
item --gap -- Serial: ${serial}
item --gap -- MAC: ${mac}
item --gap -- UUID: ${uuid}
item --gap -- Hostname: ${hostname}
item --key r dhcpip (R)etry DHCP
item --key s staticip (S)tatic IP
item --key a advancedip (A)dvanced IP settings
item reboot Reboot [Default]
choose --default reboot --timeout 30000 target && goto ${target} || goto menu

:reboot
reboot

:staticip
echo -n IP address: && read net0/ip || goto menu
echo -n Subnet mask: && read net0/netmask || goto menu
echo -n Default gateway: && read net0/gateway || goto menu
echo -n DNS server: && read net0/dns || goto menu
goto validateip

:advancedip
config net0
goto confirmnet

:confirmnet
echo ~~~ Network information ~~~
echo Hostname: ${hostname}
echo IP: ${ip}
echo Netmask: ${netmask}
echo Gateway: ${gateway}
echo DNS: ${dns}
echo -n Confirm network settings? [y/N] && read confirm || goto menu
iseq ${answer} Y && goto validateip ||
iseq ${answer} y && goto validateip ||
goto menu

:validateip
nslookup ${address} deploy.umh.app && goto settings || goto iperror

:iperror
menu Network failure
item --gap -- Serial: ${serial}
item --gap -- MAC: ${mac}
item --gap -- UUID: ${uuid}
item --gap -- Hostname: ${hostname}
item --gap -- IP: ${ip}
item --gap -- Netmask: ${netmask}
item --gap -- Gateway: ${gateway}
item --gap -- DNS: ${dns}
item --key r dhcpip (R)etry DHCP
item --key s staticip (S)tatic IP
item --key a advancedip (A)dvanced IP settings
item reboot Reboot [Default]
choose --default reboot --timeout 30000 target && goto ${target} || goto menu

:settings
console --picture http://boot.ipxe.org/texture.png
menu Install options
item --key s installsda Install to (S)DA [Default]
item --key h installhda Install to (H)DA
item --key v installvda Install to (V)DA
item --key n installnvme Install to (N)VMe
item --key c customtoken (C)ustom token
choose --default installsda --timeout 30000 target && goto ${target} || goto menu

:installsda
echo Installing to SDA
set token sda
goto boot

:installhda
echo Installing to HDA
set token hda
goto boot

:installvda
echo Installing to VDA
set token vda
goto boot

:installnvme
echo Installing to NVMe
set token nvme
goto boot

:customtoken
echo ~~~ Individual configuration ~~~
echo Please enter your customer token (or press ENTER for default):
read token || goto menu
goto boot

:boot
console
cpuid --ext 29 && set arch x86_64 || set arch i386
echo ~~~ System information ~~~
echo Serial: ${serial}
echo MAC address: ${mac}
echo UUID: ${uuid}
echo Architecture: ${arch}
echo Build architecture: ${buildarch}
echo
echo
echo ~~~ Network information ~~~
echo Hostname: ${hostname}
echo IP: ${ip}
echo Netmask: ${netmask}
echo Gateway: ${gateway}
echo DNS: ${dns}
echo
echo
chain https://deploy.umh.app/ipxe?uuid=${uuid}&mac=${mac:hexhyp}&serial=${serial}&token=${token}&arch=${arch}&buildarch=${buildarch} ||
prompt --key s --timeout 10000 Chainloading failed, hit 's' for the iPXE shell; reboot in 10 seconds && shell || reboot
